# Conversion of RGB888 image to RGB565
# lab03 of MYY505 - Computer Architecture
# Department of Computer Engineering, University of Ioannina
# Aris Efthymiou

# This directive declares subroutines. Do not remove it!
.globl rgb888_to_rgb565, showImage

.data

image888:  # A rainbow-like image Red->Green->Blue->Red
    .byte 255, 0,     0
    .byte 255,  85,   0
    .byte 255, 170,   0
    .byte 255, 255,   0
    .byte 170, 255,   0
    .byte  85, 255,   0
    .byte   0, 255,   0
    .byte   0, 255,  85
    .byte   0, 255, 170
    .byte   0, 255, 255
    .byte   0, 170, 255
    .byte   0,  85, 255
    .byte   0,   0, 255
    .byte  85,   0, 255
    .byte 170,   0, 255
    .byte 255,   0, 255
    .byte 255,   0, 170
    .byte 255,   0,  85
    .byte 255,   0,   0
# repeat the above 5 times
    .byte 255, 0,     0, 255,  85,   0 255, 170,   0, 255, 255,   0, 170, 255,   0, 85, 255,   0, 0, 255,   0, 0, 255,  85, 0, 255, 170, 0, 255, 255, 0, 170, 255, 0,  85, 255, 0,   0, 255, 85,   0, 255, 170,   0, 255, 255,   0, 255, 255,   0, 170, 255,   0,  85, 255,   0,   0
    .byte 255, 0,     0, 255,  85,   0 255, 170,   0, 255, 255,   0, 170, 255,   0, 85, 255,   0, 0, 255,   0, 0, 255,  85, 0, 255, 170, 0, 255, 255, 0, 170, 255, 0,  85, 255, 0,   0, 255, 85,   0, 255, 170,   0, 255, 255,   0, 255, 255,   0, 170, 255,   0,  85, 255,   0,   0
    .byte 255, 0,     0, 255,  85,   0 255, 170,   0, 255, 255,   0, 170, 255,   0, 85, 255,   0, 0, 255,   0, 0, 255,  85, 0, 255, 170, 0, 255, 255, 0, 170, 255, 0,  85, 255, 0,   0, 255, 85,   0, 255, 170,   0, 255, 255,   0, 255, 255,   0, 170, 255,   0,  85, 255,   0,   0
    .byte 255, 0,     0, 255,  85,   0 255, 170,   0, 255, 255,   0, 170, 255,   0, 85, 255,   0, 0, 255,   0, 0, 255,  85, 0, 255, 170, 0, 255, 255, 0, 170, 255, 0,  85, 255, 0,   0, 255, 85,   0, 255, 170,   0, 255, 255,   0, 255, 255,   0, 170, 255,   0,  85, 255,   0,   0
    .byte 255, 0,     0, 255,  85,   0 255, 170,   0, 255, 255,   0, 170, 255,   0, 85, 255,   0, 0, 255,   0, 0, 255,  85, 0, 255, 170, 0, 255, 255, 0, 170, 255, 0,  85, 255, 0,   0, 255, 85,   0, 255, 170,   0, 255, 255,   0, 255, 255,   0, 170, 255,   0,  85, 255,   0,   0

image565:
    .zero 512  # leave a 0.5Kibyte free space

.text
# -------- This is just for fun.
# Ripes has a LED matrix in the I/O tab. To enable it:
# - Go to the I/O tab and double click on LED Matrix.
# - Change the Height and Width (at top-right part of I/O window),
#     to the size of the image888 (6, 19 in this example)
# - This will enable the LED matrix
# - Uncomment the following and you should see the image on the LED matrix!
#    la   a0, image888
#    li   a1, LED_MATRIX_0_BASE
#    li   a2, LED_MATRIX_0_WIDTH
#    li   a3, LED_MATRIX_0_HEIGHT
#    jal  ra, showImage
# ----- This is where the fun part ends!

    la   a0, image888
    la   a3, image565
    li   a1, 19 # width
    li   a2,  6 # height
    jal  ra, rgb888_to_rgb565

    addi a7, zero, 10 
    ecall

# ----------------------------------------
# Subroutine showImage
# a0 - image to display on Ripes' LED matrix
# a1 - Base address of LED matrix
# a2 - Width of the image and the LED matrix
# a3 - Height of the image and the LED matrix
# Caution: Assumes the image and LED matrix have the
# same dimensions!
showImage:
    add  t0, zero, zero # row counter
showRowLoop:
    bge  t0, a3, outShowRowLoop
    add  t1, zero, zero # column counter
showColumnLoop:
    bge  t1, a2, outShowColumnLoop
    lbu  t2, 0(a0) # get red
    lbu  t3, 1(a0) # get green
    lbu  t4, 2(a0) # get blue
    slli t2, t2, 16  # place red at the 3rd byte of "led" word
    slli t3, t3, 8   #   green at the 2nd
    or   t4, t4, t3  # combine green, blue
    or   t4, t4, t2  # Add red to the above
    sw   t4, 0(a1)   # let there be light at this pixel
    addi a0, a0, 3   # move on to the next image pixel
    addi a1, a1, 4   # move on to the next LED
    addi t1, t1, 1
    j    showColumnLoop
outShowColumnLoop:
    addi t0, t0, 1
    j    showRowLoop
outShowRowLoop:
    jalr zero, ra, 0

# ----------------------------------------

rgb888_to_rgb565:
    add t0, zero, zero #arxikopoihsh t0-->counter gia tis grammes
rowLoop:
    bge t0, a2, rowsFinished #an perasei apo oles tis grammes tote termatizoume
    add t1, zero, zero #arxikopoihsh t1-->counter gia tis sthles
columnLoop:
    bge t1, a1, columnsFinished #an perasei apo ola tis sthles prepei na allaxei grammh
    lbu t2, 0(a0) #fortwnoume to kokkino
    lbu t3, 1(a0) #fortwnoume to prasino
    lbu t4, 2(a0) #fortwnoume to mple
    andi t2, t2, 0xf8 #kanoume bitwise AND gia na kratsioume ta 5 shmantikotera bits
    slli t2, t2, 8 #kanoume shift 8 bits aristera gia na paei stin teliki thesh pou tha exei sto RGB565
    andi t3, t3, 0xfc #kanoume bitwise AND gia na kratsioume ta 6 shmantikotera bits
    slli t3, t3, 3 #kanoume shift aristera 3 bits gia na paei stin teliki thesh pou tha exei sto RGB565
    srli t4, t4, 3 #kanoume shift dexia 3 bits gia na krathsoume ta 5 shmantikotera bits kai na to pame kateutheian stin thesh pou tha exei sto RBG565
    or t2, t2, t3 #kanoume OR metaxy tou R kai tou G gia na ta syndesoume 
    or t2, t2, t4 #kanoume OR mtaxy tou RG kai tou B gia na ta syndesoume
    sh t2, 0(a3) #kanoume save sto output register
    addi a0 , a0, 3 #o pointer tou input deixnei sto epomeno pixel tou RGB888 me syn3 afou exei 3 bytes ana pixel
    addi a3 , a3, 2 #o pointer tou output deixnei sto epomeno pixel tou RGB565 me syn2 afou exei 2 bytes ana pixel
    addi t1, t1, 1 #auxanoume ton pointer twn sthlwn 
    j columnLoop #ekteloume pali to loop gia na pame sthn epomenh sthlh
columnsFinished:
    addi t0, t0, 1 #auksanoume ton pointer twn grammwn afou exoume perasei apo oles tis sthles
    j rowLoop #ekteloume to loop gia tis grammes kai pame sthn epomenh grammh
rowsFinished:
    jalr zero, ra, 0
# ----------------------------------------
# Write your code here.
# You may move the "return" instruction (jalr zero, ra, 0).


